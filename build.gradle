plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'maven-publish'  // Плагин для публикации пакетов
}

group = 'com.matrix.operations'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()

    // Добавляем GitHub Packages как репозиторий
    maven {
        url = uri("https://maven.pkg.github.com/Deoul/wassup")
        credentials {
            username = project.findProperty("gpr.user") ?: System.getenv("GPR_USER")  // Получаем имя пользователя из переменной окружения
            password = project.findProperty("gpr.token") ?: System.getenv("GPR_TOKEN")  // Получаем токен из переменной окружения
        }
    }
}

dependencies {
    implementation 'org.apache.logging.log4j:log4j-api:2.17.1'
    implementation 'org.apache.logging.log4j:log4j-core:2.17.1'
    testImplementation 'junit:junit:4.13'
}

shadowJar {
    archiveBaseName.set("kursach")
    archiveClassifier.set("")
    archiveVersion.set("1.0-SNAPSHOT")

    manifest {
        attributes(
                'Main-Class': 'com.matrix.operations.MatrixOperations'
        )
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

test {
    useJUnit()  // Для JUnit 4
}

// Генерация JavaDoc
javadoc {
    destinationDir = file("$buildDir/docs/javadoc")
}

// Публикация в GitHub Packages
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            url = uri("https://maven.pkg.github.com/Deoul/wassup")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GPR_USER")
                password = project.findProperty("gpr.token") ?: System.getenv("GPR_TOKEN")
            }
        }
    }
}